      SUBROUTINE EXSNOW19(IDTS,IDT,IDA,IMN,IYR,
C     SNOW17 INPUT AND OUTPUT VARIABLES
     &                    PCP,TMP,RAIM,SNEQV,SNOW,SNOWH,
C     SNOW17 PARAMETERS
     &                    ALAT,SCF,MFMAX1,MFMIN1,UADJ1,SI,NMF1,TIPM1,
     &                    MBASE,PXTEMP,PLWHC,DAYGM,ELEV1,PA,ADC,
C     SNOW17 CARRYOVER VARIABLES
     &                    CS,TPREV)
C.......................................
C     THIS PROGRAM EXECUTES SNOW-17 ACCUMULATION/ABLATION MODEL.
C
C     WRITTEN BY QINGYUN DUAN 3/24/95
C     REVISED BY QINGYUN DUAN 7/3/01 TO INCORPORATE THE NEW SNOW SCHEME
C.......................................
      REAL PX(48),PCTS(48),RM(48)
      REAL MFMAX,MFMIN,NMF,LIQW,NEGHS,MBASE,MELT,LIQWMX,MFC
      REAL MFMAX1,MFMIN1,NMF1
      REAL ADC(11),SMFV(12),AE(2,14),CS(19)

c AWW ADDED 2016
      REAL TPREV  

      INTEGER JULDAY(12)
       COMMON/FSNWUP/IUPWE,IUPSC
      COMMON/SNCO19/WE,NEGHS,LIQW,TINDEX,ACCMAX,SB,SBAESC,SBWS,STORGE,
     1   AEADJ,NEXLAG,EXLAG(7),SNDPT,SNTMP
      COMMON/SNUP19/MFC,SFALLX,WINDC,SCTOL,WETOL,SNOF,UADJC
C
      DATA JULDAY/0,31,59,90,120,151,181,212,243,273,304,334/
C
C     PCP - PRECIPITATION (MM)
C     TMP - TEMPERATURE (DEGREE IN CELSIUS)
C     IYR - CURRENT YEAR
C     IMN - CURRENT MONTH
C     IDA - CURRENT DAY OF THE MONTH
C     IDT - COMPUTATIONAL TIME INTERVAL FOR TMP IN HOURS
C     IDTS - COMPUTATIONAL TIME INTERVAL IN SECONDS
C     KHR - CURRENT HOUR (1-24)
C     KDA - CURRENT JULIAN DAY
C     NDT - COMPUTATIONAL TIME INTERVAL FOR PCP
C
C     SNOW - SNOWFALL IN (SXFALL - MM)
C     SNEQV - SNOWWATER EQUIVALENT IN M (TWE - MM)
C     SNOWH - SNOW DEPTH IN M (SNDPT - MM)
C     SNOWT - SNOW TEMPERATURE (SNTMP - C)
C     DS    - SNOW DENSITY (DS - G/CM3)
C     TPREV - PREVIOUS TEMPERATURE (C assumed: AWW)
C.......................................
C     CONSTANT
C     IF SNOWFALL IS LESS THAN HSNOF/HR -- DO NOT
C        LEAVE CURRENT DEPLETION CURVE.
      HSNOF = 0.2
      
C      PRINT *,MFMAX1,MFMIN1
      
      KHR = IDT*IDTS/3600
      KDA = JULDAY(IMN)+IDA
      NEXLAG = 5/IDT + 2
C     I0 - DAY NUMBER FROM MARCH 21
      NDA=365
C     ADD 1 IF IT IS A LEAP YEAR
      IF (MOD(IYR,4).EQ.0 .AND. IMN .GE. 3) THEN
         KDA = KDA + 1
         NDA = NDA + 1
      END IF
C     COMPUTE DAY NUMBER FROM MARCH 21
      I0=JULDAY(3)+21
      I1=JULDAY(IMN)+IDA
      IF (KDA .GE. I0) THEN
        IDN=I1-I0
      ELSE
        IDN=NDA-(I0-I1)
      END IF
      
C.......................................
C     PARAMETER VALUES
      ELEV=ELEV1*0.01
C      PA=29.9-0.335*ELEV+0.00022*(ELEV**2.4)
C      PA=33.86*PA
      MFMAX=(MFMAX1*IDT)/6.0
      MFMIN=(MFMIN1*IDT)/6.0
      NMF=(NMF1*IDT)/6.0
      UADJ=(UADJ1*IDT)/6.0
      GM=DAYGM
      PGM=(GM*IDT)/24.0
      TIPM=1.0-((1.0-TIPM1)**(IDT/6.0))
C      PRINT *,"MFMAX 6hr",MFMAX,MFMIN     
C.......................................
C     CARRYOVER VALUES.
      WE=CS(1)
      NEGHS=CS(2)
      LIQW=CS(3)
      TINDEX=CS(4)
      ACCMAX=CS(5)
      SB=CS(6)
      SBAESC=CS(7)
      SBWS=CS(8)
      STORGE=CS(9)
      AEADJ=CS(10)
      TEX=0.0
      DO I=1,NEXLAG
         EXLAG(I)=CS(10+I)
         TEX=TEX+EXLAG(I)
      END DO
      SNDPT=CS(11+NEXLAG)
      SNTMP=CS(12+NEXLAG)

      TWE=WE+LIQW+TEX+STORGE

C     INITIALIZE HYDROMET. VARIABLES
      TA=TMP
      PX(1)=PCP
      PCTS(1)=-999.0
      RSL=-999.0
      OWE=-999.0
      OSC=-999.0

C     IFUT - 0=>OBS; 1=>FORECAST
      IFUT=0
      NDT=1
C
C   ===  INITIALIZE SNOW DATA UPDATING FLAGS  ===
C
C...SNOW WATER EQUIVALENT DATA UPDATE FLAG (NO OBSERVATION DATA)
      IUPWE = 0
C...SNOW AREAL EXTENT DATA UPDATE FLAG (NO OBSERVATION DATA)
      IUPSC = 0
C...SEASONAL SNOW MELT FACTOR VARIATION FLAG (ASSUME STANDARD CURVE)
      LMFV = 0
      SMFV = 0
C...RAIN-SNOW ELEVATION CURVE FLAG TO 0 (ASSUME NEGLIGIBLE INFLUENCE)
      LAEC = 0
      NPTAE = 14
      AE = 0
C...CORRECTION FACTORS FOR NON-RAIN MELT, SNOWFALL,AND WIND
      MFC=1.0
      UADJC=1.0
      SFALLX=1.0
      WINDC=1.0

C
      CALL PACK19(NDT,TA,PX,PCTS,RSL,OWE,OSC,PGM,RM,TWE,
C                 LAST UPDATE: 06/22/95.14:05:09 BY $WC30EA
     1            COVER,CWE,CAESC,IFUT,IDT,IDN,IMN,IYR,
C     PREVIOUS TEMPERATURE
     1            TPREV,
C     SNOWFALL (SXFALL),SNOW DENSITY (DS)
     1            SXFALL,DS,
C     SNOW17 PARAMETERS
     &            ALAT,SCF,MFMAX,MFMIN,UADJ,SI,NMF,TIPM,MBASE,
     &            PXTEMP,PLWHC,DAYGM,ADC,
C     SNOW17 
     &            PA,LMFV,SMFV,LAEC,NPTAE,AE)
C

C...Need TO UPDATE TEX AGAIN  SINCE EXSNOW19 HAS NO KNOWLEDGE OF WHAT PACK19 DID TO IT FIX 
C...COULD ALSO CHANGE THE COMMON BLOCK ON LINES 21-22, BUT THIS IS JUST AS EASY    AJN 06/18/2015
      TEX = 0.0
      DO I=1,NEXLAG
        TEX = TEX + EXLAG(I)
      END DO

      TWE=WE+LIQW+TEX+STORGE

      SNOW=SXFALL
      RAIM=RM(1)
      SNEQV=TWE/1000.
      SNOWH=SNDPT/100.
C.......................................
C     SAVE CARRYOVER VALUES.
      CS(1)=WE
      CS(2)=NEGHS
      CS(3)=LIQW
      CS(4)=TINDEX
      CS(5)=ACCMAX
      CS(6)=SB
      CS(7)=SBAESC
      CS(8)=SBWS
      CS(9)=STORGE
      CS(10)=AEADJ
      DO I=1,NEXLAG
         CS(10+I)=EXLAG(I)
      END DO
      CS(11+NEXLAG)=SNDPT
      CS(12+NEXLAG)=SNTMP
C      print*, 'exsnow19 ', SXFALL,RAIM,PLIQW,TWE,DS,TA,SNTMP,SNEQV,
C     &            SNOWH
      RETURN
      END

